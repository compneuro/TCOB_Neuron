{simulation_time = 10000, start_time([adexneuron,2],[adexmodel,0])} %for 200 ms simulation time
class adexneuron
{
  attributes
         real C;  %capacitance
   % scaling Parameters
        real Gleak;  %Leak conductance
        real Eleak;  %Leak reversal potential
        real Delta; % The slope factor
        real Vt;  %Threshold voltage

   %Bifurcation Parameters
        real A;  % Adaptation coupling parameter
        real B ;  %reset adaptation value
        real Tw;  % Adaptation Time constant;
        real Vr;  %Reset voltage

        series real W ;  %adaptation variable
        series real Vm;
        series int Flag;
        series real Varray;
        real Iin; % Input current
        real V;
        real E;
        real Dt;
    constraints
        Flag=1 --> ( C * (Vm -` Vm) /Dt=
                               - Gleak*(`Vm - Eleak) - `W + Iin
                               + Gleak*Delta*pow(E,(`Vm-Vt)/Delta)) ;
        Flag=1 --> (Tw * (W - `W)/Dt= A * (`Vm - Eleak ) - `W) ;
        Flag =1 --> Varray=Vm;
        Vm > -32  --> (Vm` = Vr) ;
        Vm> -32  --> (W`= W + B) ;
        Vm > -32  -->  Flag`=0;
        Vm <= -32  -->  Flag`=1 ;
        Flag = 0 --> Varray = 30.0;
   constructor adexneuron(Cap,GV,EE,D1,V1,A1,B1,WW,T1,Vr1,I1){
        C=Cap;
        Gleak=GV;Eleak=EE;Delta=D1;Vt=V1;A=A1;B=B1;WW=W<1>;Tw=T1;Vr=Vr1;Iin=I1;
        E = 2.71828;
        V = -60;
        Dt=0.02;
        Flag<2> = 1;
        Vm<1>  = -60;
    }
}

class adexmodel
{
    attributes
        adexneuron Adex;
    constructor adexmodel(){

       % Adex = new adexneuron(200,10.0,-70.0,2.0,-50.0,2.0,0.0,0.0,30.0,-58.0,500.0);  %parameters for tonic spiking
       Adex = new adexneuron(200,12.0,-70.0,2.0,-50.0,-10.0,0.0,0.0,300.0,-58.0,300.0);
        dump_to_file(['Vm','W'],[Adex.Varray,Adex.W]);
      % plot_graph('Output-Adex',
			  % Adex.Vm,1, 220,-80,80,1380,720,5,0.02);
    }
}
$
%Compilation and Execution commands
%load main
%[main].
%tcob2swi('adex.tcob').
%[adex].
%adexmodel(_,_).












