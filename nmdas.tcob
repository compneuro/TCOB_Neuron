% model of synapses :  NMDA Synapse
% ------------------------------------------------------------
%     Reference: Mccormick,1993
%--------------------------------------------------------------
%Authors: Manjusha Nair M, Jinesh Manchan Kannimoola@ Amrita Vishwa Vidyapeetham
%........................................................................................
{simulation_start= 2, simulation_end =5000} %for 100 ms simulation
class nmda
{
   attributes
     series real Gnmda;
     series real Inmda;
     series real SpikeTime;
     series real TimeElapsed;
       real Gmax;
       real E;
       real Vm;
       real Enmda;
       real Dt;
       real Kmg;
       real FbyRT;
       real Mgc;
       real Input;
   constraints
      TimeElapsed = Time*Dt;
      member(TimeElapsed, Input) -->  SpikeTime = Time*Dt;
      not( member(TimeElapsed, Input)) -->  SpikeTime = `SpikeTime;
      Kmg = 0.00107* pow(E,(2*0.73*Vm*FbyRT));
      TimeElapsed < Input<1> --> Gnmda = 0;
      TimeElapsed >= Input<1> --> Gnmda= Gmax* (1-(1/(1+(Kmg/Mgc))))* pow(E,-(TimeElapsed-5)/71)* ((1- pow(E,-(TimeElapsed-5)/13.2)) / 0.60);
      TimeElapsed <  Input<1> --> (Inmda = 0);
      TimeElapsed >=  Input<1> --> (Inmda  = Gnmda*  (Vm- Enmda));
     constructors nmda()
     {
      Input=[20.0];
      Gmax = 50.0;
      Enmda = 0.0;
      FbyRT = 0.038921;    % mV at room temp. T = 25 degree celsius
      Mgc = 1.5;
      Vm = -65.0;
      E = 2.718281828;
      Dt =0.02;
      SpikeTime<1> =0; %set initial value
      TimeElapsed<1> =0;
      Gnmda<1> = 0;
      Inmda<1> =0;
    }
}
class nmdasynapse
{
 attributes
      nmda Nm;
 constructors nmdasynapse()
 {
   Nm = new nmda();
   Time= 5000 --> dump_to_file(['Time','Isyn'],[Nm.TimeElapsed,Nm.Inmda]);
   Time= 5000 -->plot_graph('Inmda',Nm.Inmda,0.04, 100,-1,0,720,500,5,0.02);
  }
}
$
%.................................................................
%Use the following Compilation and Execution commands
%.................................................................
%?- [tcob2swi].
%?- tcob2swi('nmdas.tcob','nmdasynapse()').
%?- [nmdas].
%?- main(_,_).
%....................................................................




