% model of synapses :  Alpha Synapse
% ------------------------------------------------------------
%     Gsyn = Gmax * (t-t0)/tausyn)* Math.( exp(-(t-t0)/tausyn)
%      Isyn  = Gsyn *  (Vm- Esyn)
%--------------------------------------------------------------
%authors: Manjusha Nair M, Jinesh Manchan Kannimoola@ Amrita Vishwa Vidyapeetham

{simulation_start= 2, simulation_end =5000}  % for 100 ms simulation
class alpha
{
 attributes
     series real Gsyn;
     series real Isyn;
     series real SpikeTime;
     series real TimeElapsed;
     real Gmax;
     real E;
     real Tausyn;
     real Vm;
     real Esyn;
     real Dt;
     real [5] Input;
 constraints
     TimeElapsed = Time*Dt;
     member(TimeElapsed, Input) -->  SpikeTime = Time*Dt;
     not( member(TimeElapsed, Input)) -->  SpikeTime = `SpikeTime;
     TimeElapsed < Input[1] --> Gsyn = 0;
     TimeElapsed >= Input[1] --> Gsyn= Gmax * ((TimeElapsed - SpikeTime) / Tausyn)* pow(E,-( (TimeElapsed - SpikeTime) / Tausyn));
     TimeElapsed <  Input[1] --> (Isyn = 0);
     TimeElapsed >=  Input[1] --> (Isyn  = Gsyn *  (Vm- Esyn));

 constructors alpha()
  {
     Input=[20.0,25.0,30.0,35.0,40.0];
      Gmax = 3.0;
      Esyn=15.0;
      Tausyn=1.7;
      Vm = -70.0;
      E = 2.718281828;
      Dt =0.02;
      SpikeTime<1> =0; %set initial value
      TimeElapsed<1> =0;
      Gsyn<1> = 0;
      Isyn<1> =0;
  }
}
class alphasynapse
{
 attributes
      alpha Al;
 constructors alphasynapse()
 {
     Al = new alpha();
     Time= 5000 --> dump_to_file(['Time','Isyn','SpikeTime'],[Al.TimeElapsed,Al.Isyn,Al.SpikeTime]);
     Time= 5000 --> plot_graph('Alphas',Al.Isyn,0.04, 100,-100,10,720,500,20,0.02);
 }
}
$
.................................................................
Use the following Compilation and Execution commands
.................................................................
?- [tcob2swi].
?- tcob2swi('alphas.tcob','alphasynapse()').
?- [alphas].
?- main(_,_).
.....................................................................


