% model of synapses :  Double Exponential
% ------------------------------------------------------------
%     Gsyn= Gmax * Fnorm*((Math.exp(-(t - t0) / Taudecay)) - (Math.exp(-(t - t0) / Taurise)))
%      Fnorm = (1/ (-(Math.exp(-(t - t0) / Taurise)))+ (Math.exp(-(t - t0) / Taudecay)));
%      Tpeak = SpikeTime +(((Taudecay*Taurise)/(Taudecay - Taurise)) * ln(Taudecay/Taurise));
%      Isyn  = Gsyn *  (Vm- Esyn)
%--------------------------------------------------------------

%Authors: Manjusha Nair M, Jinesh Manchan Kannimoola@ Amrita Vishwa Vidyapeetham

{simulation_start= 2, simulation_end =5000} %for 100 ms simulation
class doubleexponential
{
   attributes
     series real Gsyn;
     series real Isyn;
     series real SpikeTime;
     series real TimeElapsed;
     series real Tpeak;
     series real Fnorm;
     real TT;
     real Gmax;
     real E;
     real Taudecay;
     real Taurise;
     real Vm;
     real Esyn;
     real Dt;
     real Input;
   constraints
      TimeElapsed = Time*Dt;
      log(Taudecay/Taurise, TT);
      member(TimeElapsed, Input) -->  SpikeTime = Time*Dt;
      not( member(TimeElapsed, Input)) -->  SpikeTime = `SpikeTime;
      TimeElapsed < Input[1] --> Tpeak = 0;
      TimeElapsed >= Input[1] --> Tpeak = SpikeTime +(((Taudecay*Taurise)/(Taudecay - Taurise))*TT);
      TimeElapsed < Input[1] --> Fnorm = 0;
      TimeElapsed >= Input[1] --> Fnorm = (1/ (- pow(E,-( (Tpeak - SpikeTime) / Taurise))+  pow(E,-( (Tpeak - SpikeTime) / Taudecay))));
      TimeElapsed < Input[1] --> Gsyn = 0;
      TimeElapsed >= Input[1] --> Gsyn= Gmax * Fnorm*( pow(E,-((TimeElapsed - SpikeTime) / Taudecay)) -  pow(E,-( (TimeElapsed - SpikeTime) / Taurise))) ;
      TimeElapsed <  Input[1] --> (Isyn = 0);
      TimeElapsed >=  Input[1] --> (Isyn  = Gsyn *  (Vm- Esyn));

constructors doubleexponential()
    {
      Input=[20.0];
      Gmax = 3.0;
      Esyn=15.0;
      Taudecay=3.0;
      Taurise=12.0;
      Vm = -70.0;
       E = 2.718281828;
       Dt =0.02;
       SpikeTime<1> =0; %set initial value
      TimeElapsed<1> =0;
      Gsyn<1> = 0;
      Isyn<1> =0;
  }
}
class desynapse
{
 attributes
      doubleexponential De;
 constructors desynapse()
  {
     De = new doubleexponential();
     Time= 5000 --> dump_to_file(['Time','Isyn'],[De.TimeElapsed,De.Isyn]);
     Time= 5000 -->plot_graph('DES',De.Isyn,0.04, 100,-500,0,720,500,5,0.02);
  }
}
$
%.................................................................
%Use the following Compilation and Execution commands
%.................................................................
%?- [tcob2swi].
%?- tcob2swi('des.tcob','desynapse()').
%?- [des].
%?- main(_,_).
%..................................................................


