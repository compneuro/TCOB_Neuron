% model of synapses :  Single Exponential
% ------------------------------------------------------------
%     Gsyn = Gmax * Math.exp(-(t-t0)/tausyn)
%      Isyn  = Gsyn *  (Vm- Esyn)
%--------------------------------------------------------------
%Authors: Manjusha Nair M, Jinesh Manchan Kannimoola@ Amrita Vishwa Vidyapeetham

{simulation_start= 2, simulation_end =5000} %for 100 ms simulation
class singleexponential
{
   attributes
     series real Gsyn;
     series real Isyn;
     series real SpikeTime;
     series real TimeElapsed;
       real Gmax;
       real E;
       real Tausyn;
       real Vm;
       real Esyn;
       real Dt;
       real Input;
   constraints
      TimeElapsed = Time*Dt;
      member( TimeElapsed, Input) -->  SpikeTime = Time*Dt;
      not( member(TimeElapsed, Input)) -->  SpikeTime = `SpikeTime;
      TimeElapsed < Input<1> --> Gsyn = 0;
      TimeElapsed >= Input<1> --> Gsyn= Gmax *  pow(E,-( (TimeElapsed - SpikeTime) / Tausyn));
      TimeElapsed <  Input<1> --> (Isyn = 0);
      TimeElapsed >=  Input<1> --> (Isyn  = Gsyn *  (Vm- Esyn));
   constructors singleexponential()
     {
       Input = [20.0];
       Gmax = 3.0;
       Esyn=15.0;
       Tausyn=12.0;
       Vm = -70.0;
       E = 2.718281828;
       Dt =0.02;
       SpikeTime<1> =0; %set initial value
       TimeElapsed<1> =0;
       Gsyn<1> = 0;
       Isyn<1> =0;
  }
}
class sesynapse
{
  attributes
      singleexponential Se;
  constructors sesynapse()
  {
   Se = new singleexponential();
   Time= 5000 --> dump_to_file(['Time','Isyn'],[Se.TimeElapsed,Se.Isyn]);
   Time= 5000 --> plot_graph('ses',Se.Isyn,0.04, 100,-300,0,720,500,5,0.02);
  }
}
$
.................................................................
Use the following Compilation and Execution commands
.................................................................
?- [tcob2swi].
?- tcob2swi('ses.tcob','sesynapse()').
?- [ses].
?- main(_,_).
................................................................



